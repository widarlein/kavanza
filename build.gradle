plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'maven'
}

group = 'com.github.widarlein'
group 'com.github.widarlein'
version '1.0'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.squareup.retrofit2:retrofit:2.6.1"
    implementation "com.squareup.retrofit2:converter-gson:2.6.1"
    implementation "com.squareup.okhttp3:okhttp:4.1.0"
    implementation "com.eatthepath:java-otp:0.2.0"
    implementation "com.google.guava:guava:28.0-jre"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    jdkVersion = 8
}

task copyDocs(type: Copy, dependsOn: dokka) {
    from "$buildDir/javadoc"
    into "$projectDir/docs"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'MIT License'
                    url 'https://raw.githubusercontent.com/widarlein/kavanza/master/LICENSE'
                    distribution 'repo'
                }
            }
        }
    }
}